apply plugin: 'maven-publish'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom.withXml {
                def root = asNode()

                root.dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }

                root.appendNode('name', project.name)
                root.appendNode('packaging', 'jar')
                root.appendNode('url', 'https://github.com/chikei/swagger2markup-index-ext')
                root.appendNode('description', 'A Swagger to Markup extension generates path index')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/license/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('id', 'Chikei')
                developer.appendNode('name', 'TzeKei Lee')
                developer.appendNode('email', 'chikei@gmail.com')
                developer.appendNode('organizationUrl', 'https://github.com/chikei')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/chikei/swagger2markup-index-ext.git')
                scm.appendNode('connection', 'scm:git:git://github.com/chikei/swagger2markup-index-ext.git')
                scm.appendNode('developerConnection', 'scm:git:ssh://github.com:chikei/swagger2markup-index-ext.git')
            }
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
